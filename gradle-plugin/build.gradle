plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'signing'
    id 'com.diffplug.spotless' version("6.10.0")
    id 'com.gradle.plugin-publish' version '1.3.1'
}

version = '3.0.0'
group = 'com.contrastsecurity.gradle.plugin'

repositories {
    mavenCentral()
}

gradlePlugin {
    website = 'https://www.contrastsecurity.com/'
    vcsUrl = 'https://github.com/Contrast-Security-OSS/contrast-sdk-java/tree/main/gradle-plugin'
    plugins {
        contrastSecurityPlugin {
            id = 'com.contrastsecurity.contrastplugin'
            implementationClass = 'com.contrastsecurity.gradle.plugin.ContrastGradlePlugin'
            displayName = 'Contrast Security Gradle Plugin'
            description = "Gradle plugin for Contrast Security, enables the installation of the Contrast Java Agent, connection to TeamServer, and verifying applications for new vulnerabilities."
            tags = ["Contrast", "Security"]
        }
    }
}

signing {
    setRequired {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications
    }
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

compileJava {
    options.release.set(8)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

//matched spotless conventions from the Java-agent
spotless{
    groovyGradle {
        greclipse().configFile("$rootDir/greclipse.properties")
    }

    format("misc") {
        // we have a lot of different text files - find them with git ls-files | awk -F'.' '{print $NF}' | sort | uniq
        target("*.conf", "*.config", "*.csv", "*.dtd", ".gitignore", "*.ent", "*.gradle", "*.groovy", "*.html", "*.ini", "*.js", "*.json", "*.jsp", "*.kts", "*.md", "*.properties", "*.sh", "*.template", "*.txt", "*.xlsx", "*.xml", "*.xsd", "*.yaml", "*.yml")
        endWithNewline()
    }

    java{
        googleJavaFormat("1.23.0")
    }
}

dependencies {
    implementation("com.contrastsecurity:contrast-sdk-java:3.4.2")
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

var e2eTests = "com/contrastsecurity/gradle/plugin/e2e/EndToEndTests.*"


test {
    if(!project.hasProperty("e2e")){
        exclude(e2eTests)
    }

    useJUnitPlatform()
    environment = ["CONTRAST__API__USER_NAME"      : System.getenv("CONTRAST__API__USER_NAME"),
        "CONTRAST__API__URL"            : System.getenv("CONTRAST__API__URL"),
        "CONTRAST__API__SERVICE_KEY"    : System.getenv("CONTRAST__API__SERVICE_KEY"),
        "CONTRAST__API__API_KEY"        : System.getenv("CONTRAST__API__API_KEY"),
        "CONTRAST__API__ORGANIZATION_ID": System.getenv("CONTRAST__API__ORGANIZATION_ID")]
}
